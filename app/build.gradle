plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id "kotlin-parcelize"
    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
    id "androidx.navigation.safeargs.kotlin"
}

android {
    namespace 'pl.mftau.mftau'
    defaultConfig {
        applicationId "pl.mftau.mftau"
        minSdkVersion 21
        targetSdkVersion 34
        compileSdk 34

        versionCode 27
        versionName "6.1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildFeatures {
        buildConfig = true
        dataBinding = true
        viewBinding true
        compose true
    }
    buildTypes {
        release {
            minifyEnabled = true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildTypes.each {
        Properties properties = new Properties()
        properties.load(project.rootProject.file("local.properties").newDataInputStream())
        def mftauApiUrl = properties.getProperty("mftau_api_url", "")
        it.buildConfigField("String", "MFTAU_API_URL", mftauApiUrl)
        it.resValue("string", "mftau_api_url", mftauApiUrl)
        def mftauApiKey = properties.getProperty("mftau_api_key", "")
        it.buildConfigField("String", "MFTAU_API_KEY", mftauApiKey)
        it.resValue("string", "mftau_api_key", mftauApiKey)
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.5.2"
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/atomicfu.kotlin_module']
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.appcompat:appcompat-resources:1.6.1"
    implementation "com.google.android.material:material:1.9.0"
    implementation "com.google.android.material:compose-theme-adapter:1.2.1"

    implementation "androidx.activity:activity-compose:1.8.0"
    implementation "androidx.compose.material:material:1.5.3"
    implementation "androidx.compose.material3:material3:1.1.2"
    implementation "androidx.compose.material3:material3-window-size-class:1.1.2"
    implementation "androidx.compose.animation:animation:1.5.3"
    implementation "androidx.compose.ui:ui-tooling:1.5.3"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.6.2"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.5.3"

    // Core KTX
    implementation "androidx.core:core-ktx:1.12.0"

    // Kotlin Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3"

    // ConstraintLayout
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"

    // Preferences
    implementation "androidx.preference:preference-ktx:1.2.1"
    implementation "androidx.legacy:legacy-preference-v14:1.0.0"

    // Chrome Custom Tabs
    implementation "androidx.browser:browser:1.6.0"

    // Slices
    implementation "androidx.slice:slice-builders-ktx:1.0.0-alpha08"
    implementation "androidx.slice:slice-core:1.0.0"
    implementation "androidx.annotation:annotation:1.7.0"

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.2"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.6.2"
    kapt "com.android.databinding:compiler:3.2.0-alpha10"

    // Navigation
    implementation "androidx.fragment:fragment-ktx:1.6.1"
    implementation "androidx.navigation:navigation-fragment-ktx:2.7.3"
    implementation "androidx.navigation:navigation-ui-ktx:2.7.3"

    // Room database
    implementation "androidx.room:room-runtime:2.6.0-rc01"
    implementation "androidx.room:room-ktx:2.6.0-rc01"
    kapt "androidx.room:room-compiler:2.6.0-rc01"

    // Firebase dependencies
    implementation platform("com.google.firebase:firebase-bom:31.4.0")
    implementation "com.google.firebase:firebase-core"
    implementation "com.google.firebase:firebase-auth"
    implementation "com.google.firebase:firebase-firestore"
    implementation "com.google.firebase:firebase-storage"
    implementation "com.google.firebase:firebase-crashlytics"
    implementation "com.google.firebase:firebase-analytics"
    implementation "com.firebaseui:firebase-ui-storage:3.2.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.7.3"

    // Glide
    implementation "com.github.bumptech.glide:glide:4.16.0"
    kapt "com.github.bumptech.glide:compiler:4.16.0"

    // Gson
    implementation "com.google.code.gson:gson:2.10.1"

    // Moshi
    implementation "com.squareup.moshi:moshi-kotlin:1.15.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.15.0"

    // Retrofit2
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"

    // OkHttp3
    implementation "com.squareup.okhttp3:okhttp:4.11.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.11.0"

    // PDFViewer library
    implementation "com.github.barteksc:android-pdf-viewer:3.2.0-beta.1"

    // Chart library
    implementation "com.github.PhilJay:MPAndroidChart:v3.1.0"

    // SwipeRevealLayout library
    implementation "com.chauthai.swipereveallayout:swipe-reveal-layout:1.4.1"

    // JSoup library
    implementation "org.jsoup:jsoup:1.14.3"

    // Tests
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test:runner:1.5.2"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
}
apply plugin: "com.google.gms.google-services"
